" Disable vi compatibility
set nocompatible

" PLUGINS ------------------------------------------------------ {{{

" Automatically install vim-plug
let data_dir = has('nvim') ? stdpath('data') . '/site' : '~/.vim'
if empty(glob(data_dir . '/autoload/plug.vim'))
    silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs  https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vim/plugged')

    " UI elements
    Plug 'catppuccin/vim', { 'as': 'catppuccin' }
    Plug 'itchyny/lightline.vim'
    Plug 'preservim/nerdtree'

    " Git and FZF wrappers
    Plug 'tpope/vim-fugitive'
    Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
    Plug 'junegunn/fzf.vim'

    " Tmux and vim integration
    Plug 'christoomey/vim-tmux-navigator'

call plug#end()

" }}}

" EDITOR SETTINGS ------------------------------------------------------ {{{

" --- Colorscheme ---
set termguicolors
set background=dark

" Enable syntax processing/highlighting
syntax on

" --- Spaces and tabs ---
set tabstop=4       " Number of visual spaces per tab
set shiftwidth=4    " Insert 4 spaces on a tab
set softtabstop=4   " Number of spaces in a tab when editing
set expandtab		" Use spaces (set to 4, above) instead of tabs.
set smarttab		" Let's tab key insert 'tab stops', bksp deletes tabs.
set shiftround		" Tab/shifting moves to closest tabstop
set autoindent		" Minimal automatic indenting for any filetype
set smartindent		" Indents/Dedents newlines based on rules (like C).

" --- UI Config ---
set shortmess+=I    	" Disables startup message
set number              " show line numbers
set relativenumber      " show relative numbering
set noshowmode          " no need for this when we have lightline
set cursorline          " highlight current line
filetype indent on      " load filetype-specific indent files
filetype plugin on      " load filetype specific plugin files
set wildmenu            " visual autocomplete for command menu
set wildmode=list:longest	"Lets wildmenu behave similar to Bash completion
set wildignore+=*/.git/*,*/tmp/*,*.swp
set showmatch           " highlight matching [{()}]
set laststatus=2        " Show the status line at the bottom
set mouse+=a            " A necessary evil, mouse support
set noerrorbells visualbell t_vb=    "Disable annoying error noises
set splitbelow          " Open new vertical split bottom
set splitright          " Open new horizontal splits right
set linebreak           " Have lines wrap instead of continue off-screen
set breakindent         " After linebreak, visually indents the line
set scrolloff=7         " Keep cursor in approximately the middle of the screen
set updatetime=100      " Some plugins require fast updatetime
set history=1000    	" Extends command history

" --- Intuitive behavior ---
runtime macros/matchit.vim      " Hit '%' on 'if' to jump to 'else'
set matchpairs+=<:>             " Adding matching <> to pairs list
set backspace=indent,eol,start	" Intuitive backspace behavior

" --- Buffers and files ---
set hidden		    " Allows having more than one unsaved buffer
set autoread		" Loads file automatically when it has been changed outside of vim
set undofile        " Maintain undo histroy between sessions
set undodir=~/.vim/backup

" --- Searching ---
set ignorecase		" Case insensitive search
set smartcase		" Search becomes case sensitive in the presence of uppercase letters
set incsearch		" Live incremental search
set hlsearch		" Highlights search matches

" --- Folding ---
" Use the marker method of folding
augroup filetype_vim
    autocmd!
    autocmd FileType vim setlocal foldmethod=marker
augroup END

" }}}

" CUSTOM FUNCTIONS --------------------------------------------- {{{

" }}}

" MAPPINGS ----------------------------------------------------- {{{

" Set leader key to space
nnoremap <SPACE> <Nop>
let mapleader=" "

" --- Movement ---
" Navigate split views easier (covered by tmux-vim-navigator)
"nnoremap <C-j> <C-w>j
"nnoremap <C-k> <C-w>k
"nnoremap <C-h> <C-w>h
"nnoremap <C-l> <C-w>l

" Resize split windows with arrow keys
noremap <C-up> <C-w>+
noremap <C-down> <C-w>-
noremap <C-left> <C-w><
noremap <C-right> <C-w>>

" Move to beggining/end of line without leaving home row
map H ^
map L $

" Visual line nav, allows navigating wrapped lines
nnoremap j gj
nnoremap k gk

" --- Buffers and saving ---
" For quickly switching between buffers and cycling through them.
nnoremap <leader><leader> :ls<CR>:b<space>
nnoremap <leader>b :enew<CR>
nnoremap <leader>a :buffer#<CR>
nnoremap <leader>z :bprevious<CR>
nnoremap <leader>x :bnext<CR>

" Easier saving, quiting, and save-quitting
nnoremap <leader>w :w<CR>
nnoremap <leader>q :q<CR>
nnoremap <leader>wq :wq<CR>
nnoremap <leader>Q :q!<CR>

" --- Toggleable settings ---
" Easy toggleable settings like hlsearch, spell, and paste with <leader>o<key>
let settings = {
            \ 'cc': 'cursorcolumn',
            \ 'cl': 'cursorline',
            \ 'h': 'hlsearch',
            \ 'n': 'number',
            \ 'p': 'paste',
            \ 'r': 'relativenumber',
            \ 's': 'spell',
            \ 'w': 'wrap',
            \ }

for [key,val] in items(settings)
    exec printf("nnoremap <silent> <leader>o%s :<C-U>set inv%s<CR>", key, val)
endfor

" --- Plugin mappings ---
" NERDTree
nnoremap <C-n> :NERDTreeToggle<CR>
nnoremap <leader>e :NERDTreeFocus<CR>

" FZF vim
nnoremap <leader>fw :RG<CR>
nnoremap <leader>fb :Buffers<CR>
nnoremap <leader>fh :HelpTags<CR>
nnoremap <leader>ma :Marks<CR>
nnoremap <leader>fo :History<CR>
nnoremap <leader>fz :BLines<CR>
nnoremap <leader>cm :Commits<CR>
nnoremap <leader>gt :GFiles?<CR>
nnoremap <leader>ff :Files<CR>

" --- Insert mappings ---
" Insert line above cursor in insert mode and move to it
inoremap <silent><C-o> <esc>O

" When using <CTRL-U>, <CTRL-W> or <Enter> in Insert-mode, do <CTRL-G>u
" first to start a new change so that I can undo these operations with
" u in Normal/Command mode, rather than undoing the entire Insert
" operation at once.
inoremap <C-U> <C-G>u<C-U>
inoremap <C-W> <C-G>u<C-W>
inoremap <C-J> <C-G>u<C-J>
inoremap <NL> <C-G>u<NL>
inoremap <C-M> <C-G>u<C-M>
inoremap <CR> <C-G>u<CR>

" }}}

" PLUGIN CONFIGURATION -------------------------------------------- {{{

let g:lightline = { 'colorscheme': 'catppuccin_mocha' }

" --- NERDTree ---
" Start NERDTree when Vim is started without file arguments.
autocmd StdinReadPre * let s:std_in=1
autocmd VimEnter * if argc() == 0 && !exists('s:std_in') | NERDTree | endif

" Start NERDTree when Vim starts with a directory argument.
autocmd VimEnter * if argc() == 1 && isdirectory(argv()[0]) && !exists('s:std_in') |
    \ execute 'NERDTree' argv()[0] | wincmd p | enew | execute 'cd '.argv()[0] | endif

" Exit Vim if NERDTree is the only window remaining in the only tab.
autocmd BufEnter * if tabpagenr('$') == 1 && winnr('$') == 1 && exists('b:NERDTree') && b:NERDTree.isTabTree() | call feedkeys(":quit\<CR>:\<BS>") | endif

" Close the tab if NERDTree is the only window remaining in it.
autocmd BufEnter * if winnr('$') == 1 && exists('b:NERDTree') && b:NERDTree.isTabTree() | call feedkeys(":quit\<CR>:\<BS>") | endif

" If another buffer tries to replace NERDTree, put it in the other window, and bring back NERDTree.
autocmd BufEnter * if winnr() == winnr('h') && bufname('#') =~ 'NERD_tree_\d\+' && bufname('%') !~ 'NERD_tree_\d\+' && winnr('$') > 1 |
    \ let buf=bufnr() | buffer# | execute "normal! \<C-W>w" | execute 'buffer'.buf | endif

" Open the existing NERDTree on each new tab.
autocmd BufWinEnter * if &buftype != 'quickfix' && getcmdwintype() == '' | silent NERDTreeMirror | endif

" }}}

" AUTOCOMMANDS ---------------------------------------------------- {{{

autocmd vimenter * ++nested colorscheme catppuccin_mocha

" Highlight current line, only in active window
augroup CursorLineOnlyInActiveWindow
    autocmd!
    autocmd VimEnter,WinEnter,BufWinEnter * setlocal cursorline
    autocmd WinLeave * setlocal nocursorline
augroup END

" Jump to last edit position on opening file
if has("autocmd")
    au BufReadPost * if expand('%:p') !~# '\m/\.git/' && line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
endif

" }}}
